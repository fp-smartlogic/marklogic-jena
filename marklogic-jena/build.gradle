plugins {
  id "java-library"
  id "maven-publish"
  id "signing"
  id "io.snyk.gradle.plugin.snykplugin" version "0.4"
  id "com.github.jk1.dependency-license-report" version "2.1"
}

apply plugin: 'com.marklogic.ml-gradle'

group = "com.marklogic"
version = "4.0.0"

sourceCompatibility = "11"
targetCompatibility = "11"

dependencies {
  api 'org.apache.jena:jena-arq:4.3.2'
  api 'com.marklogic:marklogic-client-api:5.5.3'

  implementation "org.slf4j:slf4j-api:1.7.36"
  implementation "ch.qos.logback:logback-classic:1.2.11"
  implementation "org.slf4j:jcl-over-slf4j:1.7.36"

  testImplementation 'com.jayway.restassured:rest-assured:2.9.0'
  testImplementation 'org.hamcrest:hamcrest-all:1.3'
}

task sourcesJar(type: Jar, dependsOn: classes) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier "javadoc"
  from javadoc
}

javadoc {
  options.overview = "src/main/java/com/marklogic/semantics/jena/overview.html"
}
// Ignores warnings on params that don't have descriptions, which is a little too noisy
javadoc.options.addStringOption('Xdoclint:none', '-quiet')


artifacts {
  archives javadocJar, sourcesJar
}

signing {
  sign configurations.archives
}

publishing {
  publications {
    mainJava(MavenPublication) {
      pom {
        name = "${group}:${project.name}"
        description = "Adapter for using MarkLogic with the jena RDF Framework"
        packaging = "jar"
        url = "https://github.com/marklogic/${project.name}"
        licenses {
          license {
            name = "The Apache License, Version 2.0"
            url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
          }
        }
        developers {
          developer {
            name = 'MarkLogic'
            email = 'java-sig@marklogic.com'
            organization = 'MarkLogic'
            organizationUrl = 'https://www.marklogic.com'
          }
          developer {
            name = 'MarkLogic Github Contributors'
            email = 'general@developer.marklogic.com'
            organization = 'Github Contributors'
            organizationUrl = 'https://github.com/marklogic/marklogic-jena/graphs/contributors'
          }
        }
        scm {
          url = "git@github.com:marklogic/${project.name}.git"
          connection = "scm:git@github.com:marklogic/${project.name}.git"
          developerConnection = "scm:git@github.com:marklogic/${project.name}.git"
        }
      }
      from components.java
      artifact sourcesJar
      artifact javadocJar
    }
  }
  repositories {
    maven {
      name = "central"
      url = mavenCentralUrl
      credentials {
        username mavenCentralUsername
        password mavenCentralPassword
      }
    }
  }
}

// See https://github.com/snyk/gradle-plugin for more information
snyk {
  //arguments = '--all-sub-projects'
  severity = 'low'
  api = snykToken
  autoDownload = true
  autoUpdate = true
}
